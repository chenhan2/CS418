<!DOCTYPE HTML> 
<html lang="en"> 
<head>
<title>MP2: Terrain</title>
<meta charset="utf-8"> 
</head>

<!-- If you add additional .js files, be sure to add them here. -->
<script src="gl-matrix-min.js"></script>
<script src="Terrain.js"></script>
<script src="MP3.js"></script>

<!-- MP2: Modify the shaders to implement Phong shading instead of Gourand! -->
<script id="shader-vs" type="x-shader/x-vertex">#version 300 es
// Vertex Shader
// Implements Gourand shading. See the lecture on "Basic Shading" for details.

// Use high-precision floats if available on this device.
#ifdef GL_FRAGMENT_PRECISION_HIGH
precision highp float;
#else
precision mediump float;
#endif

in vec3 vertexPosition;
in vec3 vertexNormal;

uniform float MinHeight;
uniform float MaxHeight;

uniform mat4 modelViewMatrix;
uniform mat4 projectionMatrix;
uniform mat3 normalMatrix;

out vec3 viewVertexPosition;
out vec3 viewVertexNormal;
out vec3 cusKDiffuse;
out vec3 cusKAmbient;


void main(void) {
    // Transform the vertex position and normal to view coordinates
    viewVertexPosition = (modelViewMatrix * vec4(vertexPosition, 1.0)).xyz;
    viewVertexNormal = normalize(normalMatrix * vertexNormal);
    
    gl_Position = projectionMatrix * modelViewMatrix * vec4(vertexPosition, 1.0);

    // Normalize height to value between 0 to 1
    float height = (vertexPosition.z - MinHeight) / (MaxHeight - MinHeight);
    if (height < 0.20) {
        cusKDiffuse = vec3(0.0/255.0, 0.0/255.0, 255.0/255.0); // blue
        cusKAmbient = vec3(0.0/255.0, 0.0/255.0, 177.0/255.0); // blue
    } else if (height < 0.40) {
        cusKDiffuse = vec3(0.0/255.0, 128.0/255.0, 0.0/255.0); // green
        cusKAmbient = vec3(0.0/255.0, 64.0/255.0, 0.0/255.0); // green
    } else if (height < 0.60) {
        cusKDiffuse = vec3(255.0/255.0, 255.0/255.0, 0.0/255.0); // yellow
        cusKAmbient = vec3(177.0/255.0, 177.0/255.0, 0.0/255.0); // yellow
    } else if (height < 0.80) {
        cusKDiffuse = vec3(255.0/255.0, 0.0/255.0, 42.0/255.0); // red
        cusKAmbient = vec3(177.0/255.0, 0.0/255.0, 21.0/255.0); // red
    } else {
        cusKDiffuse = vec3(255.0/255.0, 255.0/255.0, 255.0/255.0); // white
        cusKAmbient = vec3(255.0/255.0, 255.0/255.0, 255.0/255.0); // white
    }
}
</script>

<!-- MP2: Modify the shaders to implement Phong shading instead of Gourand! -->
<script id="shader-fs" type="x-shader/x-fragment">#version 300 es
// Fragment Shader
// Implements Gourand shading. See the lecture on "Basic Shading" for details.

// Use high-precision floats if available on this device.
#ifdef GL_FRAGMENT_PRECISION_HIGH
precision highp float;
#else
precision mediump float;
#endif

in vec3 viewVertexPosition;
in vec3 viewVertexNormal;
in vec3 cusKDiffuse;
in vec3 cusKAmbient;

uniform vec3 kAmbient;
uniform vec3 kDiffuse;
uniform vec3 kSpecular;
uniform float shininess;

uniform vec3 lightPosition;
uniform vec3 ambientLightColor;
uniform vec3 diffuseLightColor;
uniform vec3 specularLightColor;

uniform bool fog;


out vec4 fragmentColor;


void main(void) {

    // The camera is at the origin in view coordinates
    vec3 viewCameraPosition = vec3(0.0, 0.0, 0.0);
    
    // Calculate the three other vectors we need: l, r, and v
    vec3 lightVector = normalize(lightPosition - viewVertexPosition);
    vec3 reflectionVector = normalize(reflect(-lightVector, viewVertexNormal));
    vec3 viewVector = normalize(viewCameraPosition - viewVertexPosition);

    // Calculate diffuse light weighting: (n dot l)
    float diffuseWeight = max(dot(viewVertexNormal, lightVector), 0.0);
    
    // Calculate the specular light weighting: (r dot v)^(shininess)
    float rDotV = max(dot(reflectionVector, viewVector), 0.0);
    float specularWeight = pow(rDotV, shininess);

    // Sum up all three lighting components into the color for the vertex,
    // and send it to the fragment shader.
    fragmentColor = vec4(( cusKAmbient * ambientLightColor
                        + cusKDiffuse * diffuseLightColor * diffuseWeight
                        + kSpecular * specularLightColor * specularWeight), 1.0);

    
    // Set up fog
    float fogDistance = gl_FragCoord.z / gl_FragCoord.w;
    vec4 fogColor = vec4(1.0, 1.0, 1.0, 1.0);
    float fogDensity = 0.6;
                    
    #define LOG2 1.442695
    float fogAmount = 1.0 - exp2(-fogDensity * fogDensity * fogDistance * fogDistance * LOG2);
    if (fog) {
        fogAmount = clamp(fogAmount, 0.0, 1.0);
    } else {
        fogAmount = 0.0;
    }

    fragmentColor = mix(fragmentColor, fogColor, fogAmount);

}
</script>


<body onload="startup();">
    <canvas id="glCanvas" width="1200" height="600"></canvas>

    <form id="input_form">
        <fieldset>
            <legend>Rendering Options</legend>
            <div>
                <input type="radio" name="primitive" id="wireframe" value="wireframe"> Wireframe
                <input type="radio" name="primitive" id="polygon" value="polygon" checked> Polygon
                <input type="radio" name="primitive" id="wirepoly" value="wirepoly" > Polygon with Edges
            </div>
        </fieldset>
        <fieldset>
            <legend>Flight Control Direction</legend>
            <div>
                <emph>Use Keys Left and Right to adjust Roll Degree</emph> <br>
                <emph>Use Keys Up and Down to adjust Pitch Degree</emph><br>
                <emph>Use Keys + and - to adjust Speed</emph>
            </div>
        </fieldset>
        <fieldset>
            <legend>Fog</legend>
            <div>
                <input type="checkbox" id="fog" name="fog" checked><label for="fog">Enable Fog</label>
            </div>
        </fieldset>
    </form>
</body>

</html>
